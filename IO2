T 56 K [Установка адреса загрузки]

GK [Директива IO2, фиксация начального адреса подпрограммы]

[56.0] A 3 F [Пролог: формирование кода инструкции возврата в Acc]
[57.1] T 65 @ [Пролог: запись инструкции возврата]
[58.2] T 30 F [Обнуление аккумулятора]

[59.3] A 10 F [Загрузка адреса 0-го элемента]
[60.4] A 69 @ [Добавляем шаблон кода инструкции A 0 F]
[61.5] T 9 @ [Загружаем сформированную инструкцию]

[62.6] A 66 @ [Загружаем константу 0]
[63.7] T 5 F [Записываем счетчик для цикла i в ячейку 5]

[Цикл 1]

[64.8] T 30 F [Обнуление аккумулятора после прохода цикла]
[65.9] A 0 F [Загрузка в аккумулятор элемент i]
[66.10] U 8 F [Запишем его в ячейку минимума , т.к. принимаем его в начале за мин.]
[67.11] T 7 F [Запишем сам элемент]

[68.12] A 67 @ [Загружаем константу 1]
[69.13] L 0 D [Сдвиг аккумулятора на 1 разряд влево]
[70.14] A 9 @ [Прибавляем иснтрукцию загрузки элемента i]
[71.15] U 22 @ [Записываем инструкцию для загрузки в минимум]
[72.16] T 26 @ [Записываем инструкцию для загрузки элемента j]

[73.17] A 5 F [Загружаем счетчик цикла i]
[74.18] U 9 F [Запишем счетчик i , если в цикле j меньше числа i не будет]
[75.19] A 67 @ [Добавляем 1]
[76.20] T 6 F [Записываем счетчик для цикла j]

[Цикл 2]

[77.21] T 30 F [Обнуление аккумулятора после прохода цикла]
[78.22] A 0 F [Загрузка в аккумулятор элемента j]
[79.23] S 8 F [Отнимаем от элемента j число - минимум]
[80.24] E 30 @ [Получили >= 0 , значит число j не меньше минимума]

[81.25] T 30 F [Просто обнуляем аккумулятор]
[82.26] A 0 F [Загрузка в аккумулятор элемента j]
[83.27] T 8 F [Запишем его в ячейку минимума]
[84.28] A 6 F [Загружаем счетчик цикла j]
[85.29] T 9 F [Запишем счетчик , на котором был найден минимум]

[86.30] T 30 F [Просто обнуляем аккумулятор]
[87.31] A 67 @ [Загружаем константу 1]
[88.32] L 0 D [Сдвиг аккумулятора на 1 разряд влево]
[89.33] A 22 @ [Загружаем инструкцию загрузки элемента j]
[90.34] U 22 @ [Записываем новую инструкцию для следующего элемента]
[91.35] T 26 @ [Записываем новую инструкцию для загрузки в минимум]

[92.36] A 6 F [Загружаем счетчик цикла j]
[93.37] A 67 @ [Добавляем 1]
[94.38] U 6 F [Записываем новое значение счетчика]
[95.39] S 11 F [Вычитаем длину массива]
[96.40] G 21 @ [Пока < 0 проходимся по циклу]

[Конец цикла 2]

[Замена чисел]

[97.41] A 5 F [Добавляем счетчик цикла i]
[98.42] L 0 D [Сдвигаем разряд влево]
[99.43] A 10 F [Загружаем адрес 0-го элемента]
[100.44] A 68 @ [Загружаем инструкцию для записи числа T 0 F]
[101.45] T 47 @ [Записываем инструкцию в 47 @ ячейку]
[102.46] A 8 F [Загружаем найденный на этом цикле минимум]
[103.47] T 0 F [Записываем его в ячейку i]

[104.48] A 9 F [Добавляем счетчик , на котором был найден минимум]
[105.49] L 0 D [Сдвигаем разряд влево]
[106.50] A 10 F [Загружаем адрес 0-го элемента]
[107.51] A 68 @ [Загружаем инструкцию для записи числа T 0 F]
[108.52] T 54 @ [Записываем инструкцию в 54 @ ячейку]
[109.53] A 7 F [Загружаем элемент i]
[110.54] T 0 F [Записываем его в ячейку минимума]

[Конец замены чисел]

[111.55] A 67 @ [Загружаем константу 1]
[112.56] L 0 D [Сдвигаем аккумулятор на разряд влевo]
[113.57] A 9 @ [Загружаем инструкцию для загрузки элемента i]
[114.58] T 9 @ [Записываем новую инструкцию для загрузки следующего элемента]

[115.59] A 5 F [Загружаем счетчик цикла i]
[116.60] A 67 @ [Добавляем 1]
[117.61] U 5 F [Записываем новое значение счетчика]
[118.62] S 12 F [Вычитаем длину массива - 1]
[119.63] G 8 @ [Пока < 0 проходимся по циклу]

[Конец цикла 1]

[120.64] T 30 F [Обнуляем аккумулятор после выхода из циклов]
[121.65] E 0 F [Выход из подпрограммы]

[122.66] P 0 F [Константа 0]
[123.67] P 0 D [Константа 1]

[Шаблон инструкций]

[124.68] T 0 F
[125.69] A 0 F

[Начало тестовой программы]
GK [Директива IO2 , фиксация начального адреса тестовой программы]

[126.0] X 0 F [Z O F Для отладки]
[127.1] A 10 @ [Загружаем адрес 0-го элемента]
[128.2] T 10 F [Записываем в 10 ячейку]
[129.3] A 11 @ [Загружаем длину массива]
[130.4] U 11 F [Записываем в 11 ячейку]
[131.5] S 43 F [Отнимаем 1]
[132.6] T 12 F [Записываем 'длину массива - 1' в 12 ячейку]
[133.7] A 7 @ [\вызов]
[134.8] G 56 F [/подпрограммы]
[135.9] Z 0 F [Останов]

[136.10] P 12 @ [Адрес 0-го элемента]

[137.11] P 3 F [Длина массива]

[Массив]

[138.12] P 3 D [=7 0-ой элемент]
[139.13] P 7 F [=14 1-ый элемент]
[140.14] P 1 D [=3 2-ый элемент]
[141.15] P 8 D [=17 3-ый элемент]
[142.16] P 9 F [=18 4-ый элемент]
[143.17] P 6 F [=12 5-ый элемент]
EZ PF [ директива IO2, переход к исполнению ]
