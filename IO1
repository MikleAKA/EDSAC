[31] T 127 S
[32] X 0 S [Z 0 S для отладки]
[33] T 0 S [обнуляем аккум.]

[34] A 119 S [Загружаем ячейку размера массива]
[35] T 1 S [загружаем в 1-ую ячейку , обнуляем аккумулятор]

[36] A 118 S [Загружаем ячейку размера массива - 1]
[37] T 2 S [загружаем во 2-ую ячейку , обнуляем аккумулятор]

[38] A 116 S [Загружаем константу 0]
[39] T 3 S [загружаем в 3-ю ячейку , обнуляем аккумулятор]

[40] A 117 S [Загружаем константу 1]
[41] T 10 S [Записываем в 10 ячейку]

[42] A 120 S [Загружаем адресс 0-го элемента]
[43] L 0 L [Сдвиг аккумулятора на разряд влево]
[44] A 51 S [код инструкции с нулевым адрессом]
[45] T 51 S [запись сформированной инструкции + обн. акк.]

[46] A 3 S [Загружаем константу 0 в аккум.]
[47] T 11 S [загружаем в 11-ую ячейку , обнуляем аккумулятор]

[48] A 120 S [Загрузим адрес элемента 0]
[49] T 25 S [Запишем в ячейку 25 ячейку 0-го элемента]

[Цикл 1]
[50] T 23 S [Обнуляем аккумулятор после перехода]
[51] A 0 S [Инструкция для загрузки значения ячейки]
[52] U 7 S [Записываем текущий элемент]
[53] T 6 S [Запишем его в ячейку минимума , т.к. принимаем его в начале за мин.]

[54] A 25 S [Загрузим адрес элемента i]
[55] A 10 S [прибавим + 1]
[56] T 26 S [Запишем в ячейку 26 адрес элемента j]

[57] A 10 S [Загружая константу 1]
[58] L 0 L [Сдвиг аккумулятора на разряд влево]
[59] A 51 S [Загружаем инструкцию для загрузки элемента индекса i]
[60] T 67 S [Загружаем в 67 ячейку элемент индекса j]

[61] A 11 S [Загружаем счетчик]
[62] A 10 S [Добавляем 1]
[63] T 15 S [Записываем значение в ячейку 15 для цикла 2]

[64] A 25 S [загрузим адрес элемента i]
[65] T 27 S [загрузим адрес элемента i в ячейку адреса минимума , т.к. принимаем его в начале за мин.]

[Цикл 2]
[66] T 23 S [Обнуляем аккумулятор после перехода]
[67] A 0 S [Инструкция для загрузки значения ячейки]
[68] U 30 S [Запишем значение числа j в ячейку на случай , если это минимум]
[69] S 6 S [Отнимаем значение числа-минимума от значения числа j]
[70] E 76 S [Получили >= 0 , значит число j не меньше минимума]

[71] T 20 S [Просто обнуляем аккумулятор]

[72] A 30 S [Загружаем в аккумулятор число j]
[73] T 6 S [Запишем в ячейку элемента минимума]
[74] A 26 S [Загрузим адрес]
[75] T 27 S [Загузим в ячейку адреса минимума]

[76] T 20 S [просто обнуляем аккумулятор]

[77] A 10 S
[78] L 0 L [Сдвиг аккумулятора на разряд влево]
[79] A 67 S [Загружаем инструкцию для загрузки числа j в аккумулятор]
[80] T 67 S [Загружаем следующее число (j+1) в ячейку 67]

[81] A 26 S [Загрузим адресс j-ого элемента]
[82] A 10 S [На следующий обход добавим + 1]
[83] T 26 S [Запишем новый адрес]

[84] A 15 S [Загружаем индекс нынешнего элемента]
[85] A 10 S [Добавляем 1]
[86] U 15 S [Сохраняем новый индекс]
[87] S 1 S [Вычитаем длину массива]
[88] G 66 S [Пока < 0 проходимся по циклу]

[Конец Цикла 2]

[Замена чисел]

[89] A 25 S [Загружаем адрес ячейки i-го элемента]
[90] L 0 L [Сдвиг аккумулятора на разряд влево]
[91] A 114 S [Загружаем инструкцию T 0 S]
[92] T 94 S [Записываем инструкцию в 94 ячейку]
[93] A 6 S [Загружаем найденный нами в этом цикле минимум]
[94] T 0 S [Инструкция для замены значения]
[95] A 27 S [Загружаем адресс ячейки минимального элемента , найденного в данном проходе цикла]
[96] L 0 L [Сдвиг аккумулятора на разряд влево]
[97] A 114 S [Загружаем инструкцию T 0 S]
[98] T 100 S [Записываем инструкцию в 100 ячейку]
[99] A 7 S [Загружаем элемент i в этом цикле]
[100] T 0 S [Инструкция для замены значения]

[Конец замены чисел]

[101] A 10 S [Загружаем константу 1]
[102] L 0 L [Сдвиг аккумулятора на разряд влево]
[103] A 51 S [Загружаем инструкцию для загрузки числа i]
[104] T 51 S [Загружаем инструкцию для загрузки числа i+1 в ячейку 51]

[105] A 25 S [Загружаем нынешний адрес элемента i]
[106] A 10 S [Добавим 1]
[107] T 25 S [Записываем новый адрес элемента i]
[108] A 11 S [Загружаем нынешний индекс i]
[109] A 10 S [Добавляем 1]
[110] U 11 S [Записываем новый индекс i]
[111] S 2 S [Вычитаем длину массива - 1]
[112] G 50 S [Пока < 0 проходимся по циклу]

[Конец цикла 1]

[113] Z 0 S [Завершение программы]

[Шаблоны инструкций]
[114] T 0 S
[115] A 0 S

[Константы]
[116] P 0 S [0]
[117] P 0 L [1]
[118] P 2 L [Длина массива - 1]
[119] P 3 S [Длина массива ]
[120] P 60 L [Номер ячейки 0-го элемента]

[Массив]
[121] P 3 L [=7 0-ой элемент]
[122] P 7 S [=14 1-ый элемент]
[123] P 1 L [=3 2-ый элемент]
[124] P 8 L [=17 3-ый элемент]
[125] P 9 S [=18 4-ый элемент]
[126] P 6 S [=12 5-ый элемент]
